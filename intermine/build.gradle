buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
        jcenter()
    }
    dependencies {
        classpath "net.researchgate:gradle-release:2.6.0"
        classpath "com.jfrog.bintray.gradle:gradle-bintray-plugin:1.8.0"
        classpath "org.jfrog.buildinfo:build-info-extractor-gradle:4.6.2"
    }
}

subprojects {
    group = 'org.intermine'
    apply plugin: 'net.researchgate.release'
    apply plugin: "java"
    apply plugin: "maven"
    apply plugin: "com.jfrog.bintray"
    apply plugin: "com.jfrog.artifactory"
    apply plugin: "checkstyle"
    apply plugin: "eclipse"
    apply plugin: "idea"

    sourceCompatibility = 1.6
    targetCompatibility = 1.6

    repositories {
        mavenLocal()
        jcenter()

        flatDir {
            dirs '../libs'
        }
    }

    release {
        failOnCommitNeeded = true
        failOnPublishNeeded = true
        failOnSnapshotDependencies = true
        failOnUnversionedFiles = true
        failOnUpdateNeeded = true
        revertOnFail = true
        preCommitText = ''
        preTagCommitMessage = '[Gradle Release Plugin] - pre tag commit: '
        tagCommitMessage = '[Gradle Release Plugin] - creating tag: '
        newVersionCommitMessage = '[Gradle Release Plugin] - new version commit: '
        version = "2.0.0-RC-SNAPSHOT"
        tagTemplate = '${project.name}-${version}'
        versionPropertyFile = 'gradle.properties'
        versionProperties = []
        buildTasks = ['build']

        git {
            requireBranch = 'gradle'
            pushToRemote = 'origin'
            pushToBranchPrefix = ''
            commitVersionFileOnly = false
            signTag = false
        }
    }

    if (it.name != 'intermine-resources') {
        dependencies {
            compile group: 'ant', name: 'ant', version: '1.6.5'
            compile group: 'org.apache.logging.log4j', name: 'log4j-1.2-api', version: '2.10.0'
            compile group: 'org.slf4j', name: 'slf4j-simple', version: '1.7.7'
            testCompile group: 'junit', name: 'junit', version: '4.8.2'
        }

        if (it.name != 'intermine-testresources') {
            dependencies {
                compile project(':intermine-resources')
            }
        }
    }

    checkstyle {
        toolVersion '7.8.1'
        configFile file("../../config/checkstyle/checkstyle.xml")
        sourceSets = [sourceSets.main]
        showViolations = true
        ignoreFailures = false
        maxWarnings = 0
        //reportsDir = file("$project.rootDir/reports/checkstyle/$project.name")
        configProperties = [
                'checkstyle.header.file': "../config/checkstyle/copyright_header.txt",
                'suppressionFile': "../config/checkstyle/suppressions.xml"
        ]
    }

    def excludePattern = 'build/generated-src/antlr/main/'
    def excludePatternAntStyle = '**/' + excludePattern + '*'

    checkstyleMain {
        exclude excludePatternAntStyle
        source = fileTree('src/main') {
            includes = ['**/*.java']
            excludes = ['**/org/intermine/model/fulldata/**', '**/org/intermine/api/userprofile/**', 'build/**']
        }
    }

    checkstyleTest {
        enabled = false
    }

    tasks.withType(Test) {
        scanForTestClasses = false
        include '**/*Test.class'
        test.reports.html.enabled = true
    }

    test {
        ignoreFailures = true

        testLogging {
            events "skipped", "failed"
            exceptionFormat "full"
            showExceptions true
            showStackTraces true
            showCauses true
        }
    }

    artifactory {
        contextUrl = 'https://oss.jfrog.org/artifactory/'
        resolve {
            repository {
                repoKey = 'libs-release'
            }
        }
        publish {
            repository {
                if (project.version.endsWith("-SNAPSHOT")) {
                    repoKey = 'oss-snapshot-local'
                } else {
                    repoKey = 'oss-release-local'
                }
            username = System.getenv('BINTRAY_USER')
            password = System.getenv('BINTRAY_KEY')
            }
            defaults {
	        publishConfigs('archives')
            }
        }
    }

    bintray {
        user = System.getenv('BINTRAY_USER')
        key = System.getenv('BINTRAY_KEY')
        pkg {
            repo = 'intermine'
            name = project.name
            userOrg = user
            licenses = ['LGPL-2.1']
            vcsUrl = 'https://github.com/intermine/intermine.git'
        }
        configurations = ['archives']
    }

}

task testReport(type: TestReport) {
    destinationDir = file("$buildDir/reports/allTests")
    // Include the results from the `test` task in all subprojects
    reportOn subprojects*.test
}

